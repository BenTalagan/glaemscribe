\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\language "Old English"
\writing  "Runes"
\mode     "Futhorc"
\version  "0.0.2"
\authors  "Historical impl. Bertrand Bellet"

\beg changelog
  \entry "0.0.1" "Initial version"
  \entry "0.0.2" "Adding quotes handling"
\end

\charset  unicode_runes true

\beg options
  \option use_cweorth                     true
  \option use_stan                        true
  \option use_calc_and_gar                true
\end

\beg      preprocessor
  \downcase
\end

\beg      processor
    
  \beg    rules litteral
    {A}       === (a,á) 
    {E}       === (e,é) 
    {I}       === (i,í)
    {O}       === (o,ó)
    {U}       === (u,ú)
    {Y}       === (y,ý)
    {AE}      === (æ,ǽ)
    {OE}      === (œ,œ́)
    {EA}      === (ea,éa)
    {EO}      === (eo,éo)
    {IO}      === (io,ío)

    \**  VOWELS **\

    {A}       --> AC
    {E}       --> EH
    {I}       --> IS 
    {O}       --> OS
    {U}       --> UR
    {Y}       --> YR
    {AE}      --> AESC
    {OE}      --> ETHEL
    {EA}      --> EAR
    {EO}      --> EOH
    {IO}      --> IOR                    

    \**  IOTATED VOWELS **\
    {IOTABLES}    ===  {A}  * {E} * {O} * {U} * {Y} * {AE} * {OE}   * {EA}  * {EO}
    {_IOTABLES_}  ===  AC   * EH  * OS  * UR  * YR  * AESC * ETHEL  * EAR   * EOH 
    
    i [{IOTABLES}] --> GER [{_IOTABLES_} ]  

    \**  CONSONANTS **\
    (b,bb)         --> BEORC
    
	  \** (c,cc,k,kk)    --> CALC **\
    \** (ċ,ċċ)         --> CEN **\
    (c,cc,k,kk)    --> CEN
    (ċ,ċċ)         --> CEN
    \if use_calc_and_gar
      \** overload when option is on **\ 
      (c,cc,k,kk)    --> CALC
    \endif
      
    (d,dd)         --> DAEG
    (f,ff,v)       --> FEOH
    
	  \**  (g,gg,cg)      --> GAR  **\  
    \**  (ȝ,ġ,ġġ,ċġ)    --> GYFU **\
    (g,gg,cg)      --> GYFU    
    (ȝ,ġ,ġġ,ċġ)    --> GYFU
    \if use_calc_and_gar
      \** overload when option is on **\ 
      (g,gg,cg)      --> GAR
    \endif
    
    (h,hh,ç,χ)     --> HAEGL
    (j,jj,ĭ)       --> GER
    (l,ll)         --> LAGU
    (m,mm)         --> MAN
    (n,nn)         --> NYD
    (p,pp)         --> PEORTH
    (r,rr)         --> RAD
    (s,ss,z)       --> SIGEL
    (t,tt)         --> TIR
    (þ,þþ,ð,ðð)    --> THORN
    (w,ww)         --> WYNN
  
    \** OLD ENGLISH AND **\
    
    ⁊             --> OS NYD DAEG
      
    \**  CLUSTERS WITH ING **\    
    
    \** ng            --> ING GAR **\
    \** nġ            --> ING GYFU **\
    ng            --> ING GYFU
    nġ            --> ING GYFU
    \if use_calc_and_gar
      ng            --> ING GAR
    \endif
      
    \** nc            --> ING CALC **\
    \** (nċ,nk,nq)    --> ING CEN  **\
    nc            --> ING CEN
    (nċ,nk,nq)    --> ING CEN
    \if use_calc_and_gar
      ng            --> ING CALC
    \endif     
      
    \**  CLUSTERS WITH S **\   
    x             --> EOLHX
    (z,ts)        --> TIR SIGEL
    
    \**  ADDITIONAL RUNES **\    
    \if use_cweorth 
      (cw,qu,ccw,cqu)   --> CWEORTH
    \endif
    \if use_stan
      st                --> STAN
    \endif
      
  \end
  
  \beg    rules  punctuation
    , --> RUNIC_SINGLE_PUNCTUATION
    ; --> RUNIC_SINGLE_PUNCTUATION
    : --> RUNIC_MULTIPLE_PUNCTUATION
    . --> RUNIC_MULTIPLE_PUNCTUATION
    ! --> RUNIC_MULTIPLE_PUNCTUATION
    ? --> RUNIC_CROSS_PUNCTUATION 
    “ --> {NULL}
    ” --> {NULL}
    « --> {NULL}
    » --> {NULL}
  \end
\end
  
    
      