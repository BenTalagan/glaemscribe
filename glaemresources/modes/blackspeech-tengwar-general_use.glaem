\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\** BlackSpeech ring mode for glaemscribe (MAY BE INCOMPLETE) **\

\beg changelog
  \entry 0.0.1 "First version"
  \entry 0.0.2 "Ported to virtual chars."
  \entry 0.0.3 "Merging with blackspeech Annatar".
  \entry 0.0.4 "Adding double tehtar handling."
  \entry 0.0.5 "Fixing ORE/ROMEN, refactoring."
  \entry 0.0.6 "Added support for the FreeMonoTengwar font"
  \entry "0.1.0" "Added support for the Tengwar Elfica font"
  \entry "0.1.1" "Added support for inlined raw tengwar"
  \entry "0.1.2" "Added support for non-breaking spaces"   
  \entry "0.1.3" "Correcting visibility options to conform to new glaeml args strict syntax"
  \entry "0.1.4" "Added support for new unicode charsets"
  \entry "0.1.5" "Added support for the Tengwar Telcontar font"
\end

\language "Black Speech"
\writing  "Tengwar"
\mode     "Black Speech Tengwar - General Use"
\version  "0.1.5"
\authors  "J.R.R. Tolkien, impl. Talagan (Benjamin Babut)"

\world      arda
\invention  jrrt

\raw_mode "raw-tengwar"
 
\charset  tengwar_ds_sindarin false
\charset  tengwar_ds_parmaite false
\charset  tengwar_ds_eldamar  false
\charset  tengwar_ds_annatar  true
\charset  tengwar_ds_elfica   false

\charset  tengwar_guni_sindarin false
\charset  tengwar_guni_parmaite false
\charset  tengwar_guni_eldamar  false
\charset  tengwar_guni_annatar  false
\charset  tengwar_guni_elfica   false

\charset  tengwar_freemono    false
\charset  tengwar_telcontar   false

\beg      options

  \beg option reverse_o_u_tehtar O_UP_U_DOWN
    \value O_UP_U_DOWN 1
    \value U_UP_O_DOWN 2
  \end

  \beg option long_vowels_format LONG_VOWELS_USE_DOUBLE_TEHTAR
    \value LONG_VOWELS_USE_LONG_CARRIER 1
    \value LONG_VOWELS_USE_DOUBLE_TEHTAR 2
  \end  
  
  \beg option double_tehta_e true
    \visible_when "long_vowels_format == LONG_VOWELS_USE_DOUBLE_TEHTAR"
  \end
  \beg option double_tehta_i true
    \visible_when "long_vowels_format == LONG_VOWELS_USE_DOUBLE_TEHTAR"
  \end
  \beg option double_tehta_o true
    \visible_when "long_vowels_format == LONG_VOWELS_USE_DOUBLE_TEHTAR"
  \end
  \beg option double_tehta_u true
    \visible_when "long_vowels_format == LONG_VOWELS_USE_DOUBLE_TEHTAR"
  \end
  
  \beg option consonant_modification_style CONSONANT_MODIFICATION_STYLE_WAVE
    \value CONSONANT_MODIFICATION_STYLE_WAVE 0
    \value CONSONANT_MODIFICATION_STYLE_BAR 1
  \end
  
  \option reverse_numbers true
  \beg option numbers_base BASE_12
    \value    BASE_10 10
    \value    BASE_12 12
  \end
\end

\beg      preprocessor
  \** Work exclusively downcase **\
  \downcase
  
  \** Simplify trema vowels **\
  \substitute "ä" "a"
  \substitute "ë" "e"
  \substitute "ï" "i"
  \substitute "ö" "o"
  \substitute "ü" "u"
  \substitute "ÿ" "y"

  \** Dis-ambiguate long vowels **\
  \rxsubstitute "(ā|â|aa)" "á"
  \rxsubstitute "(ē|ê|ee)" "é"
  \rxsubstitute "(ī|î|ii)" "í"
  \rxsubstitute "(ō|ô|oo)" "ó"
  \rxsubstitute "(ū|û|uu)" "ú"

  \** For ORE/ROMEN **\
  \rxsubstitute "r(a|e|i|o|u|á|é|í|ó|ú)" "R\\1"

  \** Preprocess numbers **\
  \elvish_numbers "\\eval numbers_base" "\\eval reverse_numbers"
\end

\beg      processor

  \beg    rules litteral
     
    {K}                 === (c,k)
    
    \if "consonant_modification_style == CONSONANT_MODIFICATION_STYLE_WAVE"
      {GEMINATE} === GEMINATE_SIGN_TILD
      {NASAL}    === NASALIZE_SIGN_TILD
    \else
      {GEMINATE} === GEMINATE_SIGN
      {NASAL}    === NASALIZE_SIGN
    \endif
    
    {VOWELS}            === a               *  e              * i              * o              *  u
    {LVOWELS}           === á               *  é              * í              * ó              *  ú
    
    \if "reverse_o_u_tehtar == U_UP_O_DOWN"
      {O_LOOP}        === O_TEHTA
      {O_LOOP_DOUBLE} === O_TEHTA_DOUBLE
      {U_LOOP}        === U_TEHTA
      {U_LOOP_DOUBLE} === U_TEHTA_DOUBLE
    \else
      {O_LOOP}        === U_TEHTA
      {O_LOOP_DOUBLE} === U_TEHTA_DOUBLE
      {U_LOOP}        === O_TEHTA
      {U_LOOP_DOUBLE} === O_TEHTA_DOUBLE
    \endif   
       
    {_TEHTAR_}          === A_TEHTA         * E_TEHTA         * I_TEHTA       *  {O_LOOP}  * {U_LOOP}      
     
    {DIPHTHONGS}        === ai              * au              * oi          
    {_DIPHTHONGS_}      === YANTA A_TEHTA   * URE A_TEHTA     * YANTA {O_LOOP}  
      
		{WLONG}     === {NULL} \** long vowels that can be used as tehtar **\
    {_WLONG_}   === {NULL} \** tehtar of long vowels that can be used as tehtar **\
		
		{_LONG_A_}      === ARA A_TEHTA
		{_LONG_E_}      === ARA E_TEHTA	
		{_LONG_I_}      === ARA I_TEHTA
		{_LONG_O_}      === ARA {O_LOOP}	
		{_LONG_U_}      === ARA {U_LOOP}
		{_LONE_LONG_A_} === {_LONG_A_}
		{_LONE_LONG_E_} === {_LONG_E_}
		{_LONE_LONG_I_} === {_LONG_I_}
		{_LONE_LONG_O_} === {_LONG_O_}
		{_LONE_LONG_U_} === {_LONG_U_}
    
		\if "long_vowels_format == LONG_VOWELS_USE_DOUBLE_TEHTAR"
	    \if double_tehta_e
		    {_LONG_E_}       === E_TEHTA_DOUBLE
		    {_LONE_LONG_E_}  === TELCO {_LONG_E_}
				{WLONG}          === {WLONG}   * é
        {_WLONG_}        === {_WLONG_} * {_LONG_E_}
			\endif
		  \if double_tehta_i
		    {_LONG_I_}       === I_TEHTA_DOUBLE
		    {_LONE_LONG_I_}  === TELCO {_LONG_I_}
				{WLONG}          === {WLONG}   * í             
        {_WLONG_}        === {_WLONG_} * {_LONG_I_}
		  \endif
		  \if double_tehta_o
		    {_LONG_O_}       === {O_LOOP_DOUBLE}
		    {_LONE_LONG_O_}  === TELCO {_LONG_O_}
				{WLONG}          === {WLONG}   * ó             
        {_WLONG_}        === {_WLONG_} * {_LONG_O_}
		  \endif
		  \if double_tehta_u
		    {_LONG_U_}       === {U_LOOP_DOUBLE}
		    {_LONE_LONG_U_}  === TELCO {_LONG_U_}
				{WLONG}          === {WLONG}   * ú            
        {_WLONG_}        === {_WLONG_} * {_LONG_U_}
		  \endif
    \endif  
			        
    {V_D}           === [ {VOWELS} {WLONG}  ]
    {V_D_WN}        === [ {VOWELS} {WLONG} * {NULL} ]

    {_V_D_}         === [ {_TEHTAR_} {_WLONG_} ]
    {_V_D_WN_}      === [ {_TEHTAR_} {_WLONG_} * {NULL} ]
		
		\** LONE SHORT VOWELS **\
    [{VOWELS}]    --> TELCO [{_TEHTAR_}]  \** Replace isolated short vowels **\
    
		\** LONE LONG VOWELS **\	
		[{LVOWELS}]   --> [{_LONE_LONG_A_} * {_LONE_LONG_E_} * {_LONE_LONG_I_} * {_LONE_LONG_O_} * {_LONE_LONG_U_}]
    
    [{DIPHTHONGS}] -->   [{_DIPHTHONGS_}]     \**  Replace diphthongs **\
    
    \** ========================= **\
    
    {V_D_WN}p     --> PARMA {_V_D_WN_}
    {V_D_WN}t     --> TINCO {_V_D_WN_}
    {V_D_WN}{K}   --> QUESSE {_V_D_WN_}
  
    {V_D_WN} b  --> UMBAR {_V_D_WN_}
    {V_D_WN} d  --> ANDO {_V_D_WN_}
    {V_D_WN} f  --> FORMEN_EXT {_V_D_WN_} \** Beware. **\ 
    {V_D_WN} g  --> UNGWE {_V_D_WN_}
    {V_D_WN} gh --> UNGWE_EXT {_V_D_WN_}
    {V_D_WN} h  --> HYARMEN {_V_D_WN_}

    \** ======================== **\

    {K}h          --> HWESTA
    {V_D}{K}h     --> HWESTA_EXT {_V_D_} \** Take care. **\  

    \** ======================== **\

    {V_D_WN} l  --> LAMBE {_V_D_WN_} 

    \** ======================== **\

    {V_D_WN} m  --> MALTA {_V_D_WN_}
    {V_D_WN} mb --> UMBAR {NASAL} {_V_D_WN_}
    {V_D_WN} mp --> PARMA {NASAL} {_V_D_WN_}

    \** ======================== **\
    
    {V_D_WN}n   --> NUMEN {_V_D_WN_} 
    {V_D_WN}n{K}  --> QUESSE {NASAL} {_V_D_WN_} 

    \** ======================== **\
    
    \** ROMEN / ORE handling probably not accurate **\
    {V_D_WN}r   --> ORE   {_V_D_WN_}
    {V_D_WN}R   --> ROMEN {_V_D_WN_}
    
    \** ======================== **\

    s             --> SILME
    {V_D} s       --> SILME_NUQUERNA {_V_D_}
    z             --> ESSE    
    {V_D} z       --> ESSE_NUQUERNA {_V_D_}

    \** ======================== **\

    sh            --> AHA             
    {V_D} sh      --> AHA_EXT {_V_D_} \** BEWARE. **\
    

    th            --> SULE
    
    y             --> ANNA

  \end
  
  \beg rules punctuation
    . --> PUNCT_DDOT
    .. --> PUNCT_DOT PUNCT_DDOT PUNCT_DOT
    …  --> PUNCT_TILD
    ... --> PUNCT_TILD
    .... --> PUNCT_TILD
    ..... --> PUNCT_TILD
    ...... --> PUNCT_TILD
    ....... --> PUNCT_TILD

    , --> PUNCT_DOT
    : --> PUNCT_DOT
    ; --> PUNCT_DOT
    ! --> PUNCT_EXCLAM
    ? --> PUNCT_INTERR
    · --> PUNCT_DOT

    \** Apostrophe **\

    ' --> {NULL}
    ’ --> {NULL}
    
    \** NBSP **\
    {NBSP} --> NBSP
    
    \** Quotes **\

    “ --> DQUOT_OPEN
    ” --> DQUOT_CLOSE
    « --> DQUOT_OPEN 
    » --> DQUOT_CLOSE 
    
    - --> {NULL}     
    – --> PUNCT_TILD  
    — --> PUNCT_TILD
    
    [ --> PUNCT_PAREN_L
    ] --> PUNCT_PAREN_R
    ( --> PUNCT_PAREN_L
    ) --> PUNCT_PAREN_R
    { --> PUNCT_PAREN_L
    } --> PUNCT_PAREN_R
    < --> PUNCT_PAREN_L
    > --> PUNCT_PAREN_R

    \** Not universal between fonts ... **\
    $ --> ELVISH_PAREN
    ≤ --> RING_MARK_L \** Ring inscription left beautiful stuff **\
    ≥ --> RING_MARK_R \** Ring inscription right beautiful stuff **\

  \end
  
  \beg rules numbers
    0 --> NUM_0
    1 --> NUM_1
    2 --> NUM_2
    3 --> NUM_3
    4 --> NUM_4
    5 --> NUM_5
    6 --> NUM_6
    7 --> NUM_7
    8 --> NUM_8
    9 --> NUM_9
    A --> NUM_10
    B --> NUM_11      
  \end
  
\end

\beg postprocessor
  \resolve_virtuals
\end
