\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\** Sindarin Beleriand mode for glaemscribe (MAY BE INCOMPLETE) **\
\beg changelog
  \entry "0.0.2" "Added lw"
  \entry "0.0.3" "Added thorn as equivalent for th"
  \entry "0.0.4" "Porting to virtual chars to simplify and beautify"
  \entry "0.0.5" "Added charset support for Annatar"
  \entry "0.0.6" "Added support for the FreeMonoTengwar font"
\end

\language "Sindarin"
\writing  "Tengwar"
\mode     "Beleriand"
\version  "0.1.0"
\authors  "J.R.R. Tolkien, impl. Talagan (Benjamin Babut)"

\charset  tengwar_ds_sindarin true
\charset  tengwar_ds_parmaite false
\charset  tengwar_ds_eldamar  false
\charset  tengwar_ds_annatar  false
\charset  tengwar_freemono    false

\beg      options

  \beg option consonant_modification_style CONSONANT_MODIFICATION_STYLE_BAR
    \value CONSONANT_MODIFICATION_STYLE_WAVE 0
    \value CONSONANT_MODIFICATION_STYLE_BAR 1
  \end

  \option reverse_numbers true
  \beg option numbers_base BASE_12
    \value    BASE_10 10
    \value    BASE_12 12
  \end
\end

\beg      preprocessor
  \** Work exclusively downcase **\
  \downcase
  
  \** Simplify trema vowels **\
  \substitute ä a
  \substitute ë e
  \substitute ï i
  \substitute ö o
  \substitute ü u
  \substitute ÿ y

  \** We should do better for that one (todo) **\
  \substitute œ e

  \** Dis-ambiguate long vowels **\
  \rxsubstitute "(ā|â|aa)" "á"
  \rxsubstitute "(ē|ê|ee)" "é"
  \rxsubstitute "(ī|î|ii)" "í"
  \rxsubstitute "(ō|ô|oo)" "ó"
  \rxsubstitute "(ū|û|uu)" "ú"
  \rxsubstitute "(ȳ|ŷ|yy)" "ý"
  
  \** Special case of starting 'i' before vowels, replace i by j **\     
  \rxsubstitute "\\bi([aeouyáāâéēêíīîóōôúūûýȳŷ])" "j\\1"
  
  \** Preprocess numbers **\
  \elvish_numbers "\\eval numbers_base" "\\eval reverse_numbers"
\end

\beg      processor

  \beg    rules litteral
  
    \if "consonant_modification_style == CONSONANT_MODIFICATION_STYLE_WAVE"
      {GEMINATE} === GEMINATE_SIGN_TILD
      {NASAL}    === NASALIZE_SIGN_TILD
    \else
      {GEMINATE} === GEMINATE_SIGN
      {NASAL}    === NASALIZE_SIGN
    \endif
  
    {A}                 === a
    {AA}                === á
    {E}                 === e
    {EE}                === é
    {I}                 === i
    {II}                === í
    {O}                 === o
    {OO}                === ó
    {U}                 === u
    {UU}                === ú
    {Y}                 === y
    {YY}                === ý
    
    {AE}                === {A}{E}
    {AI}                === {A}{I}
    {AU}                === {A}{U}
    {AW}                === {A}w
    {EI}                === {E}{I}
    {UI}                === {U}{I}
    {OE}                === {O}{E}
             
    {K}                 === (c,k)

    \** RULES **\
    {A}                 --> OSSE
    {E}                 --> YANTA
    {I}                 --> TELCO
    {O}                 --> ANNA
    {U}                 --> URE
    
    {Y}                 --> SILME_NUQUERNA_ALT

    {AA}                --> OSSE  E_TEHTA
    {EE}                --> YANTA E_TEHTA
    {II}                --> TELCO E_TEHTA
    {OO}                --> ANNA  E_TEHTA
    {UU}                --> URE   E_TEHTA
    {YY}                --> SILME_NUQUERNA_ALT E_TEHTA

    {AE}                --> OSSE  YANTA  \** Should chose between OSSE YANTA and OSSE THSUP_TICK_INV_L. Old tengscribe had second one, amanye tenceli has first one. **\ 
    {AI}                --> OSSE  Y_TEHTA
    {AU}                --> OSSE  SEV_TEHTA 
    {AW}                --> OSSE  SEV_TEHTA
    {EI}                --> YANTA Y_TEHTA
    {UI}                --> URE   Y_TEHTA
    {OE}                --> ANNA  YANTA

    \** ======== **\
    \** 1ST LINE **\
    \** ======== **\
    {L1}         === t     * p      * {K}
    {_L1_}       === TINCO * PARMA  * CALMA

    [{L1}]       --> [{_L1_}]
 
    nt   --> TINCO {NASAL}
    mp   --> PARMA {NASAL}
    n{K} --> CALMA {NASAL}

    \** ======== **\
    \** 2ND LINE **\
    \** ======== **\
    {L2}   === d     * b     * g 
    {_L2_} === ANDO  * UMBAR * ANGA 

    [{L2}] --> [{_L2_}]

    mb   --> UMBAR  {NASAL}
    nd   --> ANDO   {NASAL}

    \** ======== **\
    \** 3RD LINE **\
    \** ======== **\
    {L3}   === (þ,th) * (f,ph,ff) * ch
    {_L3_} === SULE   * FORMEN    * AHA
     
    [{L3}] --> [{_L3_}]

    nth   --> SULE    {NASAL}
    mph   --> FORMEN  {NASAL}
    nf    --> FORMEN  {NASAL}
    nch   --> AHA     {NASAL}

    \** ======== **\
    \** 4TH LINE **\
    \** ======== **\
    {L4}    === (đ,ð,ðh,dh) * (v,bh,f_) 
    {_L4_}  === ANTO  * AMPA 

    [{L4}] --> [{_L4_}]

    \** ======== **\
    \** 5TH LINE **\
    \** ======== **\
    {L5}    === nn    * mm    * ng
    {_L5_}  === NUMEN * MALTA * NOLDO 

    [{L5}] --> [{_L5_}]

    \** ======== **\
    \** 6TH LINE **\
    \** ======== **\
    {L6}    === n   * m     * w     * _mh 
    {_L6_}  === ORE * VALA  * VILYA * MALTA_W_HOOK  

    [{L6}] --> [{_L6_}]

    \** ======== **\
    \** R/L LINE **\
    \** ======== **\
    {L_LINE}        === r     * _rh   * l     * _lh
    {_L_LINE_}      === ROMEN * ARDA  * LAMBE  * ALDA 

    [{L_LINE}] --> [{_L_LINE_}]

    \** ======== **\
    \** S/Z LINE **\
    \** ======== **\
    {S_LINE}    === s
    {_S_LINE_}  === SILME 

    [{S_LINE}] --> [{_S_LINE_}]

    ns --> SILME_NUQUERNA {NASAL}

    \** ======== **\
    \** OTHERS **\
    \** ======== **\

    j --> ARA

    h --> HYARMEN

    hw   --> HWESTA_SINDARINWA

    \** labialized consonants **\
    dw   --> ANDO   SEV_TEHTA
    gw   --> ANGA   SEV_TEHTA
    lw   --> LAMBE  SEV_TEHTA
    nw   --> ORE    SEV_TEHTA
    rw   --> ROMEN  SEV_TEHTA 

  \end
  
  \beg    rules punctuation
    . --> PUNCT_DDOT
    .. --> PUNCT_DOT PUNCT_DDOT PUNCT_DOT
    ... --> PUNCT_TILD
    …   --> PUNCT_TILD
    .... --> PUNCT_TILD
    ..... --> PUNCT_TILD
    ...... --> PUNCT_TILD
    ....... --> PUNCT_TILD

    , --> PUNCT_DOT
    : --> PUNCT_DOT
    ; --> PUNCT_DOT
    ! --> PUNCT_EXCLAM
    ? --> PUNCT_INTERR
    · --> {NULL}

    - --> {NULL}
    – --> PUNCT_TILD  
    — --> PUNCT_TILD

    \** Apostrophe **\

    ' --> {NULL}
    ’ --> {NULL}

    \** Quotes **\

    “ --> DQUOT_OPEN
    ” --> DQUOT_CLOSE
    « --> DQUOT_OPEN 
    » --> DQUOT_CLOSE 

    [ --> PUNCT_PAREN_L
    ] --> PUNCT_PAREN_R
    ( --> PUNCT_PAREN_L
    ) --> PUNCT_PAREN_R
    { --> PUNCT_PAREN_L
    } --> PUNCT_PAREN_R
    < --> PUNCT_PAREN_L
    > --> PUNCT_PAREN_R

    \** Not universal between fonts ... **\
    $ --> BOOKMARK_SIGN
    ≤ --> RING_MARK_L \** Ring inscription left beautiful stuff **\
    ≥ --> RING_MARK_R \** Ring inscription right beautiful stuff **\
  \end

  \beg    rules  numbers
    0 --> NUM_0
    1 --> NUM_1
    2 --> NUM_2
    3 --> NUM_3
    4 --> NUM_4
    5 --> NUM_5
    6 --> NUM_6
    7 --> NUM_7
    8 --> NUM_8
    9 --> NUM_9
    A --> NUM_10
    B --> NUM_11      
  \end
  
\end

\beg postprocessor
  \resolve_virtuals
\end
