\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\beg changelog
  \entry 0.0.2 "Porting to virtual chars"
\end

\language "Old English"
\writing  "Tengwar"
\mode     "Mercian"
\version  "0.0.2"
\authors  "Talagan (Benjamin Babut)"

\charset  tengwar_ds_eldamar true
\** Other charsets cannot handle all diacritics we are using here ... **\

\beg      preprocessor
  \** Work exclusively downcase **\
  \downcase
  
  \** Simplify trema vowels **\
  \substitute ä a
  \substitute ë e
  \substitute ï i
  \substitute ö o
  \substitute ü u
  \substitute ÿ y

  \substitute "ae"  "æ"
  \substitute "ea"  "æa"
  \substitute "éa"  "ǽa"
  \substitute "7"   "⁊"
  
  \** Dis-ambiguate long vowels **\
  \rxsubstitute "(ā|â|aa)" "á"
  \rxsubstitute "(ē|ê|ee)" "é"
  \rxsubstitute "(ī|î|ii)" "í"
  \rxsubstitute "(ō|ô|oo)" "ó"
  \rxsubstitute "(ū|û|uu)" "ú"
  \rxsubstitute "(ȳ|ŷ|yy)" "ý"
\end
  
\beg      processor

  \beg    rules litteral
    {A}   === a
    {AA}  === á
    {E}   === e
    {EE}  === é
    {I}   === i
    {II}  === í
    {O}   === o
    {OO}  === ó
    {U}   === u
    {UU}  === ú
    {Y}   === y
    {YY}  === ý

    {AE}    === (æ,ae)
    {AEAE}  === (ǽ,ǣ)

    {OE}    === (ø,œ)
    {OEOE}  === (ǿ,œ́)

    \** Diphtongs are always splitted, so consider we don't have any. **\
    \** @ is the phantom vowel added by the preprocessor **\

    {S_VOWELS_NP_KERNEL}   === {A} * {AE} * {OE} * {E} * {I}   * {O} * {U} * {Y}
    {S_VOWELS_WP_KERNEL}      === {S_VOWELS_NP_KERNEL} * @                           

    \** UP TEHTAS **\    
    {TEHTAR_UP_NP}  === A_TEHTA    * Y_TEHTA     * E_TEHTA_DOUBLE   * E_TEHTA   *   I_TEHTA    * O_TEHTA    * U_TEHTA   * SEV_TEHTA 
 
    {TEHTAR_UP}     === {TEHTAR_UP_NP}  * {NULL}

    \** FOR LONG VOWELS **\
    {LVOWELS}       === {AA}           * {AEAE}       * {OEOE}             * {EE}         * {II}         * {OO}          * {UU}         * {YY}  
    {LTEHTAR}       === ARA A_TEHTA    * ARA Y_TEHTA  * ARA E_TEHTA_DOUBLE * ARA E_TEHTA  * ARA I_TEHTA  * ARA O_TEHTA   * ARA U_TEHTA  * ARA SEV_TEHTA    

    {LVOWEL_MARKER}  === SEV_TEHTA_INF
  
    \** DOWN TEHTAS **\
    {DTEHTAR}        === A_TEHTA_INF   * Y_TEHTA_INF  * CIRC_TEHTA_INF     * E_TEHTA_INF  * I_TEHTA_INF  * O_TEHTA_INF  * U_TEHTA_INF   * SEV_TEHTA_INF   * {NULL}
  

    {S_VOWELS_NP}       === [ {S_VOWELS_NP_KERNEL} ]
    {S_VOWELS_NP_WN}    === [ {S_VOWELS_NP_KERNEL} * {NULL} ]
    {S_VOWELS}          === [ {S_VOWELS_WP_KERNEL} ]
    {S_VOWELS_WN}       === [ {S_VOWELS_WP_KERNEL} * {NULL} ]

    \** IMG Sheaves for all vowels **\
    {_S_VOWELS_UP_}        === [ {TEHTAR_UP} ]
    {_S_VOWELS_DN_}        === [ {DTEHTAR} ]

    {_S_VOWELS_UP_WN_}     === [ {TEHTAR_UP} * {NULL} ]
    {_S_VOWELS_DN_WN_}     === [ {DTEHTAR} * {NULL} ]

    \** Fallback rule for all vowels **\
    [ {LVOWELS} ] --> [ {LTEHTAR} ]            \** If found alone, long vowel will have a long carrier **\

    \** Fallback rule for short vowels **\
    @                   --> {NULL}                     \**  If found alone, put nothing **\
    [ {S_VOWELS_NP_KERNEL} ]   --> TELCO [ {TEHTAR_UP_NP} ]   \**  If found alone, a vowel will have a short carrier **\
    _{A}                --> VILYA                      \**  We don't want a short carrier for _a **\
    (w,u)_              --> VALA                       \**  Only resolved after vowels **\

    \** Next rule not valid in mercian mode **\
    \** {UU} -> yU - Nope in mercian **\


    \** ========== **\
    \** CONSONANTS **\
    \** ========== **\

    \** ========== **\
    \** 1st Line (Voiceless occlusives) **\
    \** Short upper dash for nasalisation **\

    {L1_KER_1}      === t     * p 
    {L1_IMG_1}      === TINCO * PARMA

    {L1_KER_2}      === ċ     * c       * k
    {L1_IMG_2}      === CALMA * QUESSE  * QUESSE
    
    {L1_KER_1_GEMS} === tt                      * pp   
    {L1_IMG_1_GEMS} === TINCO GEMINATE_DOUBLE  * PARMA GEMINATE_DOUBLE

    {L1_KER_2_GEMS} === ċċ                      * cc                      * kk 
    {L1_IMG_2_GEMS} === CALMA GEMINATE_DOUBLE  * QUESSE GEMINATE_DOUBLE * QUESSE GEMINATE_DOUBLE
  
    \** C, CV, CVV. Rules are declared to avoid conflicts between [C, NULLV, V] and [C, V, NULLV] **\
    \** We also have rules for long vowels **\
    [{L1_KER_1}]                                  --> [{L1_IMG_1}]
    [{L1_KER_2}]                                  --> [{L1_IMG_2}]
    [{L1_KER_1}]{S_VOWELS}{S_VOWELS_WN}           --> [{L1_IMG_1}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L1_KER_2}]{S_VOWELS}{S_VOWELS_WN}           --> [{L1_IMG_2}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L1_KER_1}][{LVOWELS}]                       --> [{L1_IMG_1}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}
    [{L1_KER_2}][{LVOWELS}]                       --> [{L1_IMG_2}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminateds (double sign below) **\
    \** C², C²V **\
    \** NOT C²V² (cannot put second tehta below due to gemination sign) **\
    \** NOT C²L (long vowel not possible with gemination sign) **\
    [{L1_KER_1_GEMS}]{S_VOWELS_WN}          --> [{L1_IMG_1_GEMS}]{_S_VOWELS_UP_WN_}
    [{L1_KER_2_GEMS}]{S_VOWELS_WN}          --> [{L1_IMG_2_GEMS}]{_S_VOWELS_UP_WN_}

    \** Nasals (tild above) **\
    \** nC, nCV, nCVV **\
    \** We also have rules for long vowels **\
    [ nt * mp ]                                       -->  [ TINCO NASALIZE_SIGN_TILD * PARMA NASALIZE_SIGN_TILD ]
    [ nċ * nc ]                                       -->  [ CALMA NASALIZE_SIGN_TILD * QUESSE NASALIZE_SIGN_TILD ]
    [ nt * mp ]{S_VOWELS}{S_VOWELS_WN}                -->  [ TINCO NASALIZE_SIGN_TILD * PARMA NASALIZE_SIGN_TILD ]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [ nċ * nc ]{S_VOWELS}{S_VOWELS_WN}                -->  [ CALMA NASALIZE_SIGN_TILD * QUESSE NASALIZE_SIGN_TILD ]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [ nt * mp ][{LVOWELS}]                            -->  [ TINCO NASALIZE_SIGN_TILD * PARMA NASALIZE_SIGN_TILD ][{TEHTAR_UP_NP}]{LVOWEL_MARKER}
    [ nċ * nc ][{LVOWELS}]                            -->  [ CALMA NASALIZE_SIGN_TILD * QUESSE NASALIZE_SIGN_TILD ][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** ========== **\
    \** 2nd Line (Voiced occlusives) **\
    \** Long upper dash for nasalisation **\

    {L2_KER}        === d     * b     * ġ     * g
    {L2_IMG}        === ANDO  * UMBAR * ANGA  * UNGWE
    
    {L2_KER_GEMS}   === dd                    * bb                    * (ċġ,ġġ)               * (cg,gg)
    {L2_IMG_GEMS}   === ANDO GEMINATE_DOUBLE  * UMBAR GEMINATE_DOUBLE * ANGA GEMINATE_DOUBLE  * UNGWE GEMINATE_DOUBLE

    \** C, CV, CVV **\
    [{L2_KER}]                                        --> [{L2_IMG}]
    [{L2_KER}]{S_VOWELS}{S_VOWELS_WN}                 --> [{L2_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L2_KER}][{LVOWELS}]                             --> [{L2_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign below, cannot treat diphthongs or long vowels **\
    [{L2_KER_GEMS}]{S_VOWELS_WN}                      --> [{L2_IMG_GEMS}]{_S_VOWELS_UP_WN_}

    \** Nasals, with diphthongs! **\
    [ nd * mb * nġ * ng ]                             -->  [ ANDO NASALIZE_SIGN_TILD * UMBAR NASALIZE_SIGN_TILD * ANGA NASALIZE_SIGN_TILD * UNGWE NASALIZE_SIGN_TILD ]
    [ nd * mb * nġ * ng ]{S_VOWELS}{S_VOWELS_WN}      -->  [ ANDO NASALIZE_SIGN_TILD * UMBAR NASALIZE_SIGN_TILD * ANGA NASALIZE_SIGN_TILD * UNGWE NASALIZE_SIGN_TILD ]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [ nd * mb * nġ * ng ][{LVOWELS}]                  -->  [ ANDO NASALIZE_SIGN_TILD * UMBAR NASALIZE_SIGN_TILD * ANGA NASALIZE_SIGN_TILD * UNGWE NASALIZE_SIGN_TILD ][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** ========== **\
    \** 3rd Line (Voiceless fricatives) **\
    \** Short upper dash for nasalisation **\
    {L3_KER_1}      === þ     * f
    {L3_IMG_1}      === SULE  * FORMEN
    
    {L3_KER_2}      === ç     * (χ,ħ)
    {L3_IMG_2}      === AHA   * HWESTA
    
    {L3_KER_1_GEMS} === þþ                    * ff
    {L3_IMG_1_GEMS} === SULE GEMINATE_DOUBLE * FORMEN GEMINATE_DOUBLE
    
    {L3_KER_2_GEMS} === çç                    * (χχ,ħħ)
    {L3_IMG_2_GEMS} === AHA GEMINATE_DOUBLE  * HWESTA GEMINATE_DOUBLE

    [{L3_KER_1}]                              --> [{L3_IMG_1}]
    [{L3_KER_2}]                              --> [{L3_IMG_2}]
    [{L3_KER_1}]{S_VOWELS}{S_VOWELS_WN}       --> [{L3_IMG_1}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L3_KER_2}]{S_VOWELS}{S_VOWELS_WN}       --> [{L3_IMG_2}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L3_KER_1}][{LVOWELS}]                   --> [{L3_IMG_1}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}
    [{L3_KER_2}][{LVOWELS}]                   --> [{L3_IMG_2}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign below, cannot treat diphthongs or long vowels **\
    [{L3_KER_1_GEMS}]{S_VOWELS_WN}            --> [{L3_IMG_1_GEMS}]{_S_VOWELS_UP_WN_}
    [{L3_KER_2_GEMS}]{S_VOWELS_WN}            --> [{L3_IMG_2_GEMS}]{_S_VOWELS_UP_WN_}
  
  
    \** ========== **\
    \** 4th Line (Voiced fricatives) **\
    \** Long upper dash for nasalisation **\

    {L4_KER}        === ð     * v     * j     * ȝ
    {L4_IMG}        === ANTO  * AMPA  * ANCA  * UNQUE
    
    {L4_KER_GEMS}   === ðð                    * vv                    * jj                    * ȝȝ
    {L4_IMG_GEMS}   === ANTO GEMINATE_DOUBLE  * AMPA GEMINATE_DOUBLE  * ANCA GEMINATE_DOUBLE  * UNQUE GEMINATE_DOUBLE

    [{L4_KER}]                                --> [{L4_IMG}]
    [{L4_KER}]{S_VOWELS}{S_VOWELS_WN}         --> [{L4_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L4_KER}][{LVOWELS}]                     --> [{L4_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign below, cannot treat diphthongs or long vowels **\
    [{L4_KER_GEMS}]{S_VOWELS_WN}              --> [{L4_IMG_GEMS}]{_S_VOWELS_UP_WN_}


    \** ========== **\
    \** 5th Line (Nasals) **\
    \** Long upper dash for nasalisation (wins on gemination) **\

    {L5_KER}        === n * m
    {L5_IMG}        === NUMEN * MALTA

    [{L5_KER}]                                --> [{L5_IMG}]
    [{L5_KER}]{S_VOWELS}{S_VOWELS_WN}         --> [{L5_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L5_KER}][{LVOWELS}]                     --> [{L5_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Gemination === nasalisation **\
    \** So prefer using tild to put tehtas up and down for diphthongs and long vowels **\

    [ nn * mm ]                               --> [ NUMEN NASALIZE_SIGN_TILD * MALTA NASALIZE_SIGN_TILD ]
    [ nn * mm ]{S_VOWELS}{S_VOWELS_WN}        --> [ NUMEN NASALIZE_SIGN_TILD * MALTA NASALIZE_SIGN_TILD ]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [ nn * mm ][{LVOWELS}]                    --> [ NUMEN NASALIZE_SIGN_TILD * MALTA NASALIZE_SIGN_TILD ][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** ========== **\
    \** 6th Line (Approximants == fr : Spirantes) **\
    \** Short upper dash for nasalisation **\

    {L6_KER}        === r * ĭ
    {L6_IMG}        === ORE * ANNA
    {L6_KER_GEMS}   === rr * ĭĭ
    {L6_IMG_GEMS}   === ORE GEMINATE_DOUBLE * ANNA GEMINATE_DOUBLE

    [{L6_KER}]                                --> [{L6_IMG}]
    [{L6_KER}]{S_VOWELS}{S_VOWELS_WN}         --> [{L6_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L6_KER}][{LVOWELS}]                     --> [{L6_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign below, cannot treat diphthongs or long vowels **\
    [{L6_KER_GEMS}]{S_VOWELS_WN}              --> [{L6_IMG_GEMS}]{_S_VOWELS_UP_WN_}

    \** ========== **\
    \** Liquids **\

    w                                         --> ROMEN
    w{S_VOWELS}{S_VOWELS_WN}                  --> ROMEN {_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    w[{LVOWELS}]                              --> ROMEN [{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign below, cannot treat diphthongs or long vowels **\
    ww{S_VOWELS_WN}                           --> ROMEN GEMINATE_DOUBLE {_S_VOWELS_UP_WN_}

    \** L and LL and ld are too big to receive tehtas under, so no diphthongs, no long vowels **\
    [l * ll * ld] {S_VOWELS_WN}               --> [LAMBE * LAMBE GEMINATE_DOUBLE * ALDA] {_S_VOWELS_UP_WN_}

    \** ========== **\
    \** Alveolar (sifflantes) **\
    
    {L8_KER}      ===  s * z 
    {L8_IMG}      ===  SILME_NUQUERNA * ESSE_NUQUERNA 
    {L8_KER_GEMS} ===  ss * zz 
    {L8_IMG_GEMS} ===  SILME_NUQUERNA GEMINATE_DOUBLE * ESSE_NUQUERNA GEMINATE_DOUBLE

    [{L8_KER}]                                --> [{L8_IMG}]
    [{L8_KER}]{S_VOWELS}{S_VOWELS_WN}         --> [{L8_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{L8_KER}][{LVOWELS}]                     --> [{L8_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign under, cannot treat diphthongs or long vowels **\
    [{L8_KER_GEMS}]{S_VOWELS_WN}              --> [{L8_IMG_GEMS}]{_S_VOWELS_UP_WN_}

    \**  Final s (Challenging!) **\
    s_                --> SHOOK_BEAUTIFUL                             \** Final rule for s **\
    {S_VOWELS}s_      --> TELCO {_S_VOWELS_UP_} SILME_NUQUERNA     \** Rule es_ **\
    [{LVOWELS}]s_     --> [{LTEHTAR}] SILME_NUQUERNA                \** Rule és_ **\

    s --> SILME    \** Overload lonely s **\
    z --> ESSE    \** Overload lonely z **\

    \** ========== **\
    \** Ligatures **\
    
    {LINE_VARIOUS_1_KER}      ===  sċ 
    {LINE_VARIOUS_1_IMG}      ===  ANCA_CLOSED  
    {LINE_VARIOUS_2_KER}      ===  hw * çt * (χt,ħt)
    {LINE_VARIOUS_2_IMG}      ===  HARP_SHAPED  * AHA_TINCO  * HWESTA_TINCO  

    [{LINE_VARIOUS_1_KER}]                                --> [{LINE_VARIOUS_1_IMG}]
    [{LINE_VARIOUS_2_KER}]                                --> [{LINE_VARIOUS_2_IMG}]
    [{LINE_VARIOUS_1_KER}]{S_VOWELS}{S_VOWELS_WN}         --> [{LINE_VARIOUS_1_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{LINE_VARIOUS_2_KER}]{S_VOWELS}{S_VOWELS_WN}         --> [{LINE_VARIOUS_2_IMG}]{_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    [{LINE_VARIOUS_1_KER}][{LVOWELS}]                     --> [{LINE_VARIOUS_1_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}
    [{LINE_VARIOUS_2_KER}][{LVOWELS}]                     --> [{LINE_VARIOUS_2_IMG}][{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** No nasals, no geminated **\

    \** ========== **\
    \** Various **\
   
    h                                            --> HYARMEN
    h{S_VOWELS}{S_VOWELS_WN}                     --> HYARMEN {_S_VOWELS_UP_}{_S_VOWELS_DN_WN_}
    h[{LVOWELS}]                                 --> HYARMEN [{TEHTAR_UP_NP}]{LVOWEL_MARKER}

    \** Geminated sign below, cannot treat diphthongs or long vowels **\
    hh{S_VOWELS_WN}                              --> HYARMEN GEMINATE_DOUBLE {_S_VOWELS_UP_WN_}

    \** ========== **\
    \** X **\
    \** For x, due to the cedilla, we cannot put tehtas under the tengwa (so no need to treat diphthongs or long vowels) **\

    x{S_VOWELS_WN}                               --> QUESSE ALVEOLAR_SIGN {_S_VOWELS_UP_WN_}
    xx{S_VOWELS_WN}                              --> QUESSE ALVEOLAR_SIGN GEMINATE_DOUBLE {_S_VOWELS_UP_WN_}
    nx{S_VOWELS_WN}                              --> QUESSE ALVEOLAR_SIGN NASALIZE_SIGN_TILD {_S_VOWELS_UP_WN_}     
  \end
  
  \beg    rules punctuation 
    ⁊ --> OLD_ENGLISH_AND

    . --> PUNCT_DDOT
    .. --> PUNCT_DOT PUNCT_DDOT PUNCT_DOT
    …  --> PUNCT_TILD
    ... --> PUNCT_TILD
    .... --> PUNCT_TILD
    ..... --> PUNCT_TILD
    ...... --> PUNCT_TILD
    ....... --> PUNCT_TILD

    , --> PUNCT_DOT
    : --> PUNCT_DOT
    ; --> PUNCT_DOT
    ! --> PUNCT_EXCLAM
    ? --> PUNCT_INTERR
    · --> PUNCT_DOT

    \** Apostrophe **\

    ' --> {NULL}
    ’ --> {NULL}

    \** Quotes **\

    “ --> DQUOT_OPEN
    ” --> DQUOT_CLOSE
    « --> DQUOT_OPEN 
    » --> DQUOT_CLOSE 

    - --> PUNCT_DOT    
    – --> PUNCT_TILD  
    — --> PUNCT_TILD

    [ --> PUNCT_PAREN_L
    ] --> PUNCT_PAREN_R
    ( --> PUNCT_PAREN_L_ALT \** TODO : Remove alt ? **\
    ) --> PUNCT_PAREN_R_ALT \** TODO : Remove alt ? **\
    { --> PUNCT_PAREN_L
    } --> PUNCT_PAREN_R
    < --> PUNCT_PAREN_L
    > --> PUNCT_PAREN_R  

    \** Not universal between fonts ... **\
    $ --> BOOKMARK_SIGN
    ≤ --> RING_MARK_L \** Ring inscription left beautiful stuff **\
    ≥ --> RING_MARK_R \** Ring inscription right beautiful stuff **\
  \end  
\end

\beg postprocessor
  \resolve_virtuals
\end