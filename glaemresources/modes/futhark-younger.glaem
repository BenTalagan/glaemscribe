\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\beg changelog
  \entry "0.0.1" "Initial version"
  \entry "0.0.2" "Adding quotes handling"
\end

\language "Old Norse"
\writing  "Runes"
\mode     "Younger Futhark"
\version  "0.0.2"
\authors  "Historical impl. Bertrand Bellet"

\world      primary_related_to_arda
\invention  historical

\charset  unicode_runes true

\beg options 
  \beg option runic_style RUNIC_STYLE_LONG_BRANCH 
    \value RUNIC_STYLE_LONG_BRANCH 0
    \value RUNIC_STYLE_SHORT_TWIG  1
  \end
\end

\beg      preprocessor
  \downcase
  
  \** Dis-ambiguate q,c,k **\
  \rxsubstitute "(q|c|k)" "k"
\end

\beg      processor
    
  \beg    rules litteral
    {A}       === (a,á,æ,ǽ,ę) 
    {I}       === (i,í,j,e,é)
    {O}       === (ą,ą́,ö,ǫ,ǫ́,ǭ)
    {U}       === (u,ú,o,ó,y,ý,ø,ǿ,œ)
    {AI}      === (æi,ei)
  	{AU}      === (au,ǫu,ey,æy,øy,œy)

    \**  VOWELS **\
    \if "runic_style == RUNIC_STYLE_LONG_BRANCH"
      {AR}      === LONG_BRANCH_AR
      {OSS}     === LONG_BRANCH_OSS     
    \else
      {AR}      === SHORT_TWIG_AR
      {OSS}     === SHORT_TWIG_OSS
    \endif
    
    {A}       --> {AR}
    {I}       --> ISS 
    {O}       --> {OSS}
    {U}       --> UR
    {AI}      --> {AR} ISS 
	  {AU}      --> {OSS} UR
    
    \** CONSONANTS **\
    \if "runic_style == RUNIC_STYLE_LONG_BRANCH"
      {BJARKAN} === BJARKAN
      {HAGALL}  === LONG_BRANCH_HAGALL
      {MADR}    === LONG_BRANCH_MADR
      {NAUD}    === NAUD
      {YR}      === LONG_BRANCH_YR
      {SOL}     === LONG_BRANCH_SOL
      {TYR}     === TYR
    \else
      {BJARKAN} === SHORT_TWIG_BJARKAN
      {HAGALL}  === SHORT_TWIG_HAGALL
      {MADR}    === SHORT_TWIG_MADR
      {NAUD}    === SHORT_TWIG_NAUD
      {YR}      === SHORT_TWIG_YR
      {SOL}     === SHORT_TWIG_SOL
      {TYR}     === SHORT_TWIG_TYR
    \endif
    
    (b,bb,mb,p,pp,mp) --> {BJARKAN}
    (f,ff)            --> FE
    _g                --> KAUN      \** # Initial / After prefix **\
    (g,g_)            --> {HAGALL}  \** # Median or final spirant **\
    (h,hh)            --> {HAGALL}
    (k,nk,kk,gg,ng)   --> KAUN
    (l,ll)            --> LOGR
    (m,mm)            --> {MADR}
    (n,nn)            --> {NAUD}
    (r,rr)            --> REID
    (ř)               --> {YR} \**  ř < Germanic z **\
    (s,ss)            --> {SOL}
	  (t,tt,nt,d,dd,nd) --> {TYR}
    (þ,þþ,ð,ðð)       --> THURS
    (v,vv,w,ww)       --> URUZ
    x                 --> KAUN {SOL}                 
    z                 --> {TYR} {SOL}
	
    \**  nasalization of a **\
		{A}(m,mm)        --> {OSS} {MADR}
		{A}(n,nn)        --> {OSS} {NAUD}
		{A}(mp,mb)       --> {OSS} {BJARKAN}
		{A}(nt,nd)       --> {OSS} {TYR}
		{A}(nk,ng)       --> {OSS} KAUN				
  \end
  
  \beg    rules  punctuation
    , --> RUNIC_SINGLE_PUNCTUATION
    ; --> RUNIC_SINGLE_PUNCTUATION
    : --> RUNIC_MULTIPLE_PUNCTUATION
    . --> RUNIC_MULTIPLE_PUNCTUATION
    ! --> RUNIC_MULTIPLE_PUNCTUATION
    ? --> RUNIC_CROSS_PUNCTUATION	
    “ --> {NULL}
    ” --> {NULL}
    « --> {NULL} 
    » --> {NULL} 
  \end
\end
  
    
      