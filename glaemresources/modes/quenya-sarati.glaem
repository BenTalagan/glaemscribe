\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\** Changelog **\
\**
\beg changelog
  \entry "0.0.2", "Moved outspace character to general element"
\end
**\

\language "Quenya"
\writing  "Sarati"
\mode     "Quenya Usage"
\version  "0.0.1"
\authors  "J.R.R. Tolkien, impl. Talagan (Benjamin Babut)"

\charset  sarati_eldamar true

\outspace SARATI_SPACE

\beg      preprocessor
  \** Work exclusively downcase **\
  \downcase
  
  \** Simplify trema vowels **\
  \** Removed ï and ü, not quite sure how to treat them for qenya : ex : oïkta diphthong or not ?  **\
  \substitute ä a
  \substitute ë e
  \substitute ö o
  \substitute ÿ y
  
  \** Dis-ambiguate long vowels **\
  \rxsubstitute "(ā|â|aa)"          "á"
  \rxsubstitute "(ē|ê)"             "é" \** ee is allowed in qenya **\
  \rxsubstitute "(ī|î|iï|ïi|ïï|ii)" "í"
  \rxsubstitute "(ō|ô|oo)"          "ó"
  \rxsubstitute "(ū|û|uü|üu|üü|uu)" "ú"
  \rxsubstitute "(ȳ|ŷ|yy)"          "ý"

  \substitute   "iu" "iw"
  \substitute   "eu" "ew"
  \substitute   "au" "aw"
  \substitute   "ai" "ay"
  \substitute   "oi" "oy"
  \substitute   "ui" "uy"

  \** Split long a  **\
  \substitute   "á" "aa" 

  \** Dis-ambiguate qu **\
  \substitute   "qu" "q" 
\end

\beg      processor

  
  \beg    rules litteral 
    {A}                           === a
    {AA}                          === aa     
    {E}                           === e
    {EE}                          === é     
    {I}                           === (i,ï) 
    {II}                          === í     
    {O}                           === o 
    {OO}                          === ó     
    {U}                           === (u,ü) 
    {UU}                          === ú     
                                      
    {K}                           === (c,k)
    {MB}                          === (b,mb)
    {SS}                          === (z,ss)

    {VOWELS}                      === {A}                             * {E}                           * {I}                             * {O}                             * {U}
    {LVOWELS}                     === {AA}                            * {EE}                          * {II}                            * {OO}                            * {UU}
    {TEHTAS_}                     === SARATI_QUENYA_A                 * SARATI_QUENYA_E               * SARATI_QUENYA_I                 * SARATI_QUENYA_O                 * SARATI_QUENYA_U 
    {STEHTAS}                     === {NULL}                          * SARATI_QUENYA_E               * SARATI_QUENYA_I                 * SARATI_QUENYA_O                 * SARATI_QUENYA_U 
    {LTEHTAS}                     === SARATI_QUENYA_A SARATI_DASH_U   * SARATI_QUENYA_E SARATI_DASH_U * SARATI_QUENYA_I SARATI_DASH_U   * SARATI_QUENYA_O SARATI_DASH_U   * SARATI_QUENYA_U SARATI_DASH_U 
    {LTEHTAS_FOR_CONSONANTS}      === SARATI_QUENYA_A                 * SARATI_QUENYA_E SARATI_DASH_U * SARATI_QUENYA_I SARATI_DASH_U   * SARATI_QUENYA_O SARATI_DASH_U   * SARATI_QUENYA_U SARATI_DASH_U

    {V_L_KER_WN}                  === [ {VOWELS}  * {LVOWELS}                 * {NULL} ]
    {V_IMG_FOR_CONSONNANTS_WN}    === [ {STEHTAS} * {LTEHTAS_FOR_CONSONANTS}  * SARATI_DOT_D ] \** No vowel == dot below **\

    \**  RULES   **\

    [{VOWELS}]            -->   [{TEHTAS_}] SARATI_QUENYA_LONG_VOWEL_CARRIER  \** Isolated vowels : use short carrier (reversed order RTL) **\
    [{LVOWELS}]           -->   [{LTEHTAS}] SARATI_QUENYA_LONG_VOWEL_CARRIER  \** Long vowels: carrier + dash + tehta **\

    \**  FIRST LINE **\

    {L1_KER}              === t         * p         * {K}             * q                 * tt                      * pp                      * {K}{K}  
    {L1_IMG}              === SARATI_T  * SARATI_P  * SARATI_QUENYA_C * SARATI_QUENYA_QU  * SARATI_DASH_D SARATI_T  * SARATI_DASH_D SARATI_P  * SARATI_DASH_D SARATI_QUENYA_C 

    [{L1_KER}]{V_L_KER_WN}    --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{L1_IMG}]

    ty{V_L_KER_WN}            --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_TY
    ts{V_L_KER_WN}            --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_TS

    \** Missing py, ps? **\

    \**  SECOND LINE **\

    {L2_KER}        === nd                * {MB}              * ng        * ngw
    {L2_IMG}        === SARATI_QUENYA_ND  * SARATI_QUENYA_MB  * SARATI_NG * SARATI_QUENYA_NGW

    [{L2_KER}]{V_L_KER_WN}    --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{L2_IMG}]

    ndy{V_L_KER_WN}           --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_NDY

    \** ########### **\
    \**  THIRD LINE **\

    {L3_KER}        === th                * f                   * h         *  hw
    {L3_IMG}        === SARATI_QUENYA_S   * SARATI_QUENYA_F_ALT * SARATI_H  *  SARATI_QUENYA_HW

    [{L3_KER}]{V_L_KER_WN}    --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{L3_IMG}]

    hy{V_L_KER_WN}            --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_HY

    \** # The two following are not treated the same way in tengwar **\
    ht{V_L_KER_WN}            --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_HT_ALT_1
    hty{V_L_KER_WN}           --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_HTY

    \** ########### **\
    \**  FOURTH LINE **\

    {LINE_4TH_KER}        === nt                * mp                * nc                * nq \** # Not nqu, due to preprocessor **\
    {LINE_4TH_IMG}        === SARATI_QUENYA_NT  * SARATI_QUENYA_MP  * SARATI_QUENYA_NC  * SARATI_QUENYA_NQU
    
    [{LINE_4TH_KER}]{V_L_KER_WN}  --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{LINE_4TH_IMG}]
    nty{V_L_KER_WN}               --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_NTY

    \** ########### **\
    \**  FIFTH LINE **\

    {LINE_5TH_KER}        === n         * m         * ñ                         * ñw                * _nw                 * nn                      * mm
    {LINE_5TH_IMG}        === SARATI_N  * SARATI_M  * SARATI_QUENYA_VELAR_NASAL * SARATI_QUENYA_NW  * SARATI_QUENYA_NW    * SARATI_DASH_D SARATI_N  * SARATI_DASH_D SARATI_M

    [{LINE_5TH_KER}]{V_L_KER_WN}  --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{LINE_5TH_IMG}]
    ny{V_L_KER_WN}                -->  {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_NY

    \** Missing my ? **\

    \** ########### **\
    \**  SIXTH LINE **\

    {LINE_6TH_KER}                === r        * v                    * y               * w         * rr  
    {LINE_6TH_IMG}                === SARATI_R * SARATI_QUENYA_V_ALT  * SARATI_QUENYA_Y * SARATI_W  * SARATI_DASH_D SARATI_R 

    [{LINE_6TH_KER}]{V_L_KER_WN}  --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{LINE_6TH_IMG}]

    \** Weak r is not distinguished **\
    \** Missing ry? rd?  **\

    \** ########### **\
    \**  L Line **\

    {LINE_L_KER}                === l         * ll                      * d
    {LINE_L_IMG}                === SARATI_L  * SARATI_DASH_D SARATI_L  * SARATI_D

    [{LINE_L_KER}]{V_L_KER_WN}  --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{LINE_L_IMG}]

    \**  Missing ld, ly, hl, hr ? **\

    \** ########### **\
    \**  S/Z line **\

    \** st v **\
    \** sty … **\
    \** ss ¦ ou w ou i  **\

    \**  For s, use the same sarat as for th **\

    {LINE_8TH_KER}        === s                 * {SS}   
    {LINE_8TH_IMG}        === SARATI_QUENYA_S   * SARATI_QUENYA_SS_ALT_1 

    [{LINE_8TH_KER}]{V_L_KER_WN} --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{LINE_8TH_IMG}]

    {LINE_8PTH_KER}        === st                 * sty
    {LINE_8PTH_IMG}        === SARATI_QUENYA_ST   * SARATI_QUENYA_STY 

    [{LINE_8PTH_KER}]{V_L_KER_WN} --> 2,1 --> {V_IMG_FOR_CONSONNANTS_WN}[{LINE_8PTH_IMG}]

    \**  Override lonely s / ss / before consonant: TODO!!!! **\
    \**  s      -> 8  **\
    \**  {SS}     -> ,  **\

    \** ############ **\
    \**  OTHERS **\

    x {V_L_KER_WN}   --> {V_IMG_FOR_CONSONNANTS_WN} SARATI_QUENYA_X
  \end
    
  \beg    rules punctuation
    · --> {NULL}
    , --> {NULL}
    ; --> {NULL}
    : --> {NULL}
    . --> SARATI_SPACE
    - --> SARATI_SPACE
    – --> SARATI_SPACE
    ! --> {NULL}
    ? --> {NULL}
    ' --> {NULL}
    [ --> {NULL}
    ] --> {NULL}
    ‘ --> {NULL}
    ’ --> {NULL}
    “ --> {NULL}
    ” --> {NULL}
  \end
  
\end

\beg postprocessor
  \reverse
\end

\**  Ponctuation **\



\** ############### **\
\**  Helpers, transcribed from amanye tenceli **\

\** ########### **\
\** # p Z **\
\** # t " **\
\** # c # **\
\** # q p **\

\** # ty ± **\
\** # ts g **\

\** ########### **\
\** # mb _ **\
\** # nd € **\
\** # ndy ³ **\
\** # ng & **\
\** # ngw s **\

\** ########### **\

\** # f \ ou [ ## Aside or below **\
\** # s (th) Ÿ **\
\** # hy ½ **\
\** # h Ë **\
\** # hw º **\

\** # ht ² ou ‚ **\
\** # hty Œ **\

\** ########### **\

\** # mp d **\
\** # nt ª **\
\** # nty „ **\
\** # nc — **\
\** # nq ˜ **\

\** ########### **\

\** # m P **\
\** # n À **\
\** # ny ‰ **\
\** # ñ + **\
\** # nw , **\

\** ########### **\

\** # v a ou ` ## Aside or below **\
\** # r F **\
\** # y » **\
\** # w ¹ **\

\** ########### **\

\** # l ? **\

\** ########### **\

\** # st v **\
\** # sty … **\
\** # ss ¦ ou w ou i  **\

\** ################ **\

\** # x (ks) y **\


\** # GEMINATION -> ó **\
\** # PRECEDING S -> ý **\
\** # SHORT CARRIER -> È **\
\** #  **\
\** # Vowels : **\
\** # Stop Vowel: Ó **\
\** # i   Ò **\
\** # e   è ou Ô **\
\** # a   Ö **\
\** # o   Ü **\
\** # u   Þ **\
\** #  **\
\** # Long Vowels: **\
\** # Carrier dash : ò **\
\** # Always use carrier dashes EXCEPT for a **\

      

