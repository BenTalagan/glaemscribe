\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\beg changelog
  \entry "0.0.2" "Correcting ts/ps sequences to work better with eldamar"
  \entry "0.0.3" "Porting to virtual chars"
  \entry "0.0.4" "Added charset support for Annatar"
  \entry "0.0.5" "Added support for the FreeMonoTengwar font" 
  \entry "0.1.0" "Added support for the Tengwar Elfica font"
  \entry "0.1.1" "Added support for inlined raw tengwar"  
  \entry "0.1.2" "Added support for non-breaking spaces"   
  \entry "0.1.3" "Added support for new unicode charsets"   
  \entry "0.1.4" "Added support for the Tengwar Telcontar font"
\end

\**  Westron mode for glaemscribe (MAY BE INCOMPLETE) **\
\language Westron
\writing  Tengwar
\mode     "Westron Tengwar - G*"
\version  "0.1.4"
\authors  "Talagan (Benjamin Babut), based on J.R.R. Tolkien"

\world      arda
\invention  experimental

\raw_mode "raw-tengwar"

\charset  tengwar_ds_sindarin true
\charset  tengwar_ds_parmaite false
\charset  tengwar_ds_eldamar  false
\charset  tengwar_ds_annatar  false
\charset  tengwar_ds_elfica   false

\charset  tengwar_guni_sindarin false
\charset  tengwar_guni_parmaite false
\charset  tengwar_guni_eldamar  false
\charset  tengwar_guni_annatar  false
\charset  tengwar_guni_elfica   false

\charset  tengwar_freemono    false
\charset  tengwar_telcontar   false

\beg      options

  \beg option reverse_o_u_tehtar U_UP_O_DOWN
    \value O_UP_U_DOWN 1
    \value U_UP_O_DOWN 2
  \end
  \beg option consonant_modification_style CONSONANT_MODIFICATION_STYLE_BAR
    \value CONSONANT_MODIFICATION_STYLE_WAVE 0
    \value CONSONANT_MODIFICATION_STYLE_BAR 1
  \end

  \option reverse_numbers true
  \beg option numbers_base BASE_12
    \value    BASE_10 10
    \value    BASE_12 12
  \end
  
\end

\beg      preprocessor
  \** Work exclusively downcase **\
  \downcase
  
  \** Simplify trema vowels **\
  \substitute "ä" "a"
  \substitute "ë" "e"
  \substitute "ï" "i"
  \substitute "ö" "o"
  \substitute "ü" "u"
  \substitute "ÿ" "y"
  
  \** Dis-ambiguate long vowels **\
  \rxsubstitute "(ā|â|aa)" "á"
  \rxsubstitute "(ē|ê|ee)" "é"
  \rxsubstitute "(ī|î|ii)" "í"
  \rxsubstitute "(ō|ô|oo)" "ó"
  \rxsubstitute "(ū|û|uu)" "ú"
  \rxsubstitute "(ȳ|ŷ|yy)" "ý"
  
  \** Preprocess numbers **\
  \elvish_numbers "\\eval numbers_base" "\\eval reverse_numbers"
\end

\beg      processor

  \beg    rules litteral  
  
    \if "consonant_modification_style == CONSONANT_MODIFICATION_STYLE_WAVE"
      {GEMINATE} === GEMINATE_SIGN_TILD
      {NASAL}    === NASALIZE_SIGN_TILD
    \else
      {GEMINATE} === GEMINATE_SIGN
      {NASAL}    === NASALIZE_SIGN
    \endif
    
    \if "reverse_o_u_tehtar == U_UP_O_DOWN"
      {O_LOOP}        === O_TEHTA
      {O_LOOP_DOUBLE} === O_TEHTA_DOUBLE
      {U_LOOP}        === U_TEHTA
      {U_LOOP_DOUBLE} === U_TEHTA_DOUBLE
    \else
      {O_LOOP}        === U_TEHTA
      {O_LOOP_DOUBLE} === U_TEHTA_DOUBLE
      {U_LOOP}        === O_TEHTA
      {U_LOOP_DOUBLE} === O_TEHTA_DOUBLE
    \endif
  
    {A}   === a
    {AA}  === á
    {E}   === e
    {EE}  === é
    {I}   === i
    {II}  === í
    {O}   === o
    {OO}  === ó
    {U}   === u
    {UU}  === ú

    \** Short diphthongs **\
    {AI}  === {A}{I}
    {AU}  === {A}{U}
	  {EI}  === {E}{I}
	  {EU}  === {E}{U}
	  {OI}  === {O}{I}
	  {OU}  === {O}{U}
	  {UI}  === {U}{I}
	  {IU}  === {I}{U}

    \** LONG diphthongs **\      
    {AAI} === {AA}{I} \** âi **\
    {AAU} === {AA}{U} \** âu **\
    {EEI} === {EE}{I} \** êi **\
    {EEU} === {EE}{U} \** êu **\
    {OOI} === {OO}{I} \** ôi **\
    {OOU} === {OO}{U} \** ôu **\

    {SDIPHTHONGS}  === {AI}           * {AU}          * {EI} 				    * {EU}        * {IU}        * {OI}          * {OU}        * {UI}
    {_SDIPHTONGS_} === YANTA A_TEHTA  * URE A_TEHTA   * YANTA E_TEHTA	  * URE E_TEHTA * URE I_TEHTA * YANTA {O_LOOP} * URE {O_LOOP} * YANTA {U_LOOP}                   
    
    {LDIPHTHONGS}  === {AAI}              * {AAU}              * {EEI}              * {EEU}            * {OOI}              * {OOU}
    {_LDIPHTONGS_} === ARA A_TEHTA YANTA  * ARA A_TEHTA URE    * ARA E_TEHTA YANTA  * ARA E_TEHTA URE  * ARA {O_LOOP} YANTA  * ARA {O_LOOP} URE
                   
    {VOWELS}      === {A}      * {E}      * {I}        * {O}        * {U}    
    {TEHTAR}      === A_TEHTA  * E_TEHTA  * I_TEHTA    * {O_LOOP}    * {U_LOOP}
                  
    {LVOWELS}     === {AA}         * {EE}         * {II}         * {OO}         * {UU}
    {LTETHAR}     === ARA A_TEHTA  * ARA E_TEHTA  * ARA I_TEHTA  * ARA {O_LOOP}  * ARA {U_LOOP} 

    \** Let' put all vowels/diphthongs in the same basket **\
    {V_D}         === [ {VOWELS}  * {LVOWELS} * {SDIPHTHONGS} * {LDIPHTHONGS} ]        
    \** And their images... **\            
    {_V_D_}       === [ {TEHTAR}  * {LTETHAR} * {_SDIPHTONGS_} * {_LDIPHTONGS_} ]
 
    [{VOWELS}]      --> TELCO [{TEHTAR}]   \** Replace isolated short vowels **\
    [{LVOWELS}]     --> [{LTETHAR}]    \** Replace long vowels **\
    [{SDIPHTHONGS}]  --> [{_SDIPHTONGS_}]  \** Replace short diphthongs **\
    [{LDIPHTHONGS}]  --> [{_LDIPHTONGS_}]  \** Replace long diphthongs **\

    \** ================ **\
    \**    CONSONANTS    **\
    \** ================ **\     

    {L1_S}         === t      * p     * ch		  * (c,k)       
    {L1_T}         === TINCO  * PARMA * CALMA	  * QUESSE 
    
    [{L1_S}]       -->  [ {L1_T} ]
    [{L1_S}]{V_D}  -->  [ {L1_T} ]{_V_D_} 
	
    {L1_G_S}         === tt			           * pp               * cch				        * (c,k)(c,k)             
    {L1_G_T}         === TINCO {GEMINATE}  * PARMA {GEMINATE} * CALMA {GEMINATE}	* QUESSE {GEMINATE}  
    
    [{L1_G_S}]       -->  [ {L1_G_T} ]
    [{L1_G_S}]{V_D}  -->  [ {L1_G_T} ]{_V_D_} 
	  
    {L1_N_S}         === nt			        * mp              * nch				    * (n,ñ)(c,k)             
    {L1_N_T}         === TINCO {NASAL}  * PARMA {NASAL}   * CALMA {NASAL} * QUESSE {NASAL}  
    
    [{L1_N_S}]       -->  [ {L1_N_T} ]
    [{L1_N_S}]{V_D}  -->  [ {L1_N_T} ]{_V_D_} 	 

    {L2_S}         === d    * b     * j	  	* g
    {L2_T}         === ANDO * UMBAR * ANGA	* UNGWE
		
    [{L2_S}]       --> [{L2_T}] 
    [{L2_S}]{V_D}  --> [{L2_T}]{_V_D_} 

    {L2_G_S}         === dd              * bb               * jj			         * gg
    {L2_G_T}         === ANDO {GEMINATE} * UMBAR {GEMINATE} * ANGA {GEMINATE}  * UNGWE {GEMINATE}
		
    [{L2_G_S}]       --> [{L2_G_T}] 
    [{L2_G_S}]{V_D}  --> [{L2_G_T}]{_V_D_} 

    {L2_N_S}         === nd           * mb            * nj			      * (n,ñ)g
    {L2_N_T}         === ANDO {NASAL} * UMBAR {NASAL} * ANGA {NASAL}  * UNGWE {NASAL}
		
    [{L2_N_S}]       --> [{L2_N_T}] 
    [{L2_N_S}]{V_D}  --> [{L2_N_T}]{_V_D_} 

    \** Alignment of tehta is not the same in the font **\
    \** So we need to split the third line unfortunately **\
    {L3_1_S}          === (th,þ)    * (f,ph)      
    {L3_1_T}          === SULE      * FORMEN  
   
    {L3_2_S}          === sh     * kh     
    {L3_2_T}          === AHA    * HWESTA
   
    [{L3_1_S}]        --> [{L3_1_T}] 
    [{L3_1_S}]{V_D}   --> [{L3_1_T}]{_V_D_} 
    [{L3_2_S}]        --> [{L3_2_T}] 
    [{L3_2_S}]{V_D}   --> [{L3_2_T}]{_V_D_} 
		
    {L3_1G_S}         === (thth,tth,þþ)    * (ff,phph,pph)
    {L3_1G_T}         === SULE {GEMINATE}  * FORMEN {GEMINATE}
   
    {L3_2G_S}          === (shsh,ssh)      * (k,kh)kh
    {L3_2G_T}          === AHA {GEMINATE}  * HWESTA {GEMINATE}
   
    [{L3_1G_S}]        --> [{L3_1G_T}] 
    [{L3_1G_S}]{V_D}   --> [{L3_1G_T}]{_V_D_} 
    [{L3_2G_S}]        --> [{L3_2G_T}] 
    [{L3_2G_S}]{V_D}   --> [{L3_2G_T}]{_V_D_} 		

    {L3_1N_S}          === (nth,nþ)     * (nf,mf,mph)      
    {L3_1N_T}          === SULE {NASAL} * FORMEN {NASAL}  
   
    {L3_2N_S}          === nsh         * (n,ñ)kh     
    {L3_2N_T}          === AHA {NASAL} * HWESTA {NASAL}
   
    [{L3_1N_S}]        --> [{L3_1N_T}] 
    [{L3_1N_S}]{V_D}   --> [{L3_1N_T}]{_V_D_} 
    [{L3_2N_S}]        --> [{L3_2N_T}] 
    [{L3_2N_S}]{V_D}   --> [{L3_2N_T}]{_V_D_} 

    {L4_S}            === (dh,ð)    * v   	* zh	  * gh
    {L4_T}            === ANTO      * AMPA  * ANCA	* UNQUE
		
    [{L4_S}]          --> [{L4_T}] 
    [{L4_S}]{V_D}     --> [{L4_T}]{_V_D_} 

    {L4_G_S}            === (dh,ð)(dh,ð)     * vv               * (zhzh,zzh)	     * (ghgh,ggh)
    {L4_G_T}            === ANTO {GEMINATE}  * AMPA {GEMINATE}  * ANCA {GEMINATE}  * UNQUE {GEMINATE}
		
    [{L4_G_S}]          --> [{L4_G_T}] 
    [{L4_G_S}]{V_D}     --> [{L4_G_T}]{_V_D_} 

    {L4_N_S}            === n(dh,ð)       * (mv,nv)       * nzh	          * (n,ñ)gh
    {L4_N_T}            === ANTO {NASAL}  * AMPA {NASAL}  * ANCA {NASAL}  * UNQUE {NASAL}
		
    [{L4_N_S}]          --> [{L4_N_T}] 
    [{L4_N_S}]{V_D}     --> [{L4_N_T}]{_V_D_} 

    {L5_S}            === n     * m     * ny     * ñ
    {L5_T}            === NUMEN * MALTA * NOLDO  * NWALME
		
    [{L5_S}]          --> [{L5_T}] 
    [{L5_S}]{V_D}     --> [{L5_T}]{_V_D_} 

    {L5_G_S}            === nn      * mn      * (nyny,nny)   * ññ
    {L5_G_T}            === NUMEN   * MALTA   * NOLDO        * NWALME
		
    [{L5_G_S}]          --> [{L5_G_T}] 
    [{L5_G_S}]{V_D}     --> [{L5_G_T}]{_V_D_} 
		
    {L6_S}            === w  	  * y     * rr               * ww         	    * yy
    {L6_T}            === VALA  * ANNA  * ROMEN {GEMINATE} * VALA {GEMINATE}  * ANNA {GEMINATE}
    [r * {L6_S}]      --> [ ORE   * {L6_T}] 
    [r * {L6_S}]{V_D} --> [ ROMEN * {L6_T}]{_V_D_} 

    \** This one is not useful (redundant with higher) **\
    \** Keep it for clarity of mind **\
    r_                --> ORE

    s{V_D}            --> SILME_NUQUERNA {_V_D_}  \** Before a vowel goes down **\
    s                 --> SILME                   \** Any other pos, up **\
    z{V_D}            --> ESSE_NUQUERNA {_V_D_}   \** Before a vowel goes down **\
    z                 --> ESSE                    \** Any other pos, up **\
		
    ns{V_D}           --> SILME_NUQUERNA {NASAL} {_V_D_}
    ns                --> SILME_NUQUERNA {NASAL}                   
    nz{V_D}           --> ESSE_NUQUERNA {NASAL} {_V_D_}   
    nz                --> ESSE_NUQUERNA {NASAL}                

    ts                --> TINCO SARINCE
    ps                --> PARMA SARINCE
    (ks,cs,x)         --> QUESSE SARINCE

    ts{V_D}           --> TINCO SARINCE {_V_D_}  
    ps{V_D}           --> PARMA SARINCE {_V_D_}
    (ks,cs,x){V_D}    --> QUESSE SARINCE {_V_D_}	

    h{V_D}            --> HYARMEN {_V_D_}
    h                 --> HYARMEN
    hh{V_D}           --> HYARMEN {GEMINATE} {_V_D_}
    hh                --> HYARMEN {GEMINATE}
                      
    l{V_D}            --> LAMBE {_V_D_}
    l                 --> LAMBE
                      
    ll{V_D}           --> LAMBE {GEMINATE} {_V_D_}
    ll                --> LAMBE {GEMINATE}
		
    (hl,lh){V_D}      --> ALDA {_V_D_}
    (hl,lh)           --> ALDA		

    (hr,rh){V_D}      --> ARDA {_V_D_}
    (hr,rh)           --> ARDA	
		
  \end
  
  \beg rules punctutation
    . --> PUNCT_DDOT
    .. --> PUNCT_DOT PUNCT_DDOT PUNCT_DOT
    …  --> PUNCT_TILD
    ... --> PUNCT_TILD
    .... --> PUNCT_TILD
    ..... --> PUNCT_TILD
    ...... --> PUNCT_TILD
    ....... --> PUNCT_TILD

    , --> PUNCT_DOT
    : --> PUNCT_DOT
    ; --> PUNCT_DOT
    ! --> PUNCT_EXCLAM
    ? --> PUNCT_INTERR
    · --> PUNCT_DOT

    \** Apostrophe **\

    ' --> {NULL}
    ’ --> {NULL}
    
    \** NBSP **\
    {NBSP} --> NBSP
    
    \** Quotes **\

    “ --> DQUOT_OPEN
    ” --> DQUOT_CLOSE
    « --> DQUOT_OPEN 
    » --> DQUOT_CLOSE 
    
    - --> PUNCT_DOT    
    – --> PUNCT_TILD  
    — --> PUNCT_DTILD
 
    [ --> PUNCT_PAREN_L
    ] --> PUNCT_PAREN_R
    ( --> PUNCT_PAREN_L
    ) --> PUNCT_PAREN_R
    { --> PUNCT_PAREN_L
    } --> PUNCT_PAREN_R
    < --> PUNCT_PAREN_L
    > --> PUNCT_PAREN_R  

    \** Not universal between fonts ... **\
    $ --> BOOKMARK_SIGN
    ≤ --> RING_MARK_L \** Ring inscription left beautiful stuff **\
    ≥ --> RING_MARK_R \** Ring inscription right beautiful stuff **\
  \end

  \beg rules numbers
    0 --> NUM_0
    1 --> NUM_1
    2 --> NUM_2
    3 --> NUM_3
    4 --> NUM_4
    5 --> NUM_5
    6 --> NUM_6
    7 --> NUM_7
    8 --> NUM_8
    9 --> NUM_9
    A --> NUM_10
    B --> NUM_11   
  \end
  
\end

\beg postprocessor
  \resolve_virtuals
\end