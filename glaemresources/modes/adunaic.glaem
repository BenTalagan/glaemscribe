\**

Glǽmscribe (also written Glaemscribe) is a software dedicated to
the transcription of texts between writing systems, and more 
specifically dedicated to the transcription of J.R.R. Tolkien's 
invented languages to some of his devised writing systems.

Copyright (C) 2015 Benjamin Babut (Talagan).

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License as published by
the Free Software Foundation, either version 3 of the License, or
any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License for more details.

You should have received a copy of the GNU Affero General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

**\

\beg changelog
 \entry "0.0.2" "Added option for o/u tehtar loop orientation"
 \entry "0.0.3" "Normalizing to virtual chars" 
 \entry "0.0.4" "Added charset support for Annatar"
 \entry "0.0.5" "Added support for the FreeMonoTengwar font"
 \entry "0.0.6" "Added wave/bar option for consonant modifications sign"
\end

\**  Adunaic mode for glaemscribe (MAY BE INCOMPLETE) **\
\language Adûnaic
\writing  Tengwar
\mode     Glaemscrafu
\version  "0.1.0"
\authors  "Talagan (Benjamin Babut), based on J.R.R Tolkien"

\charset  tengwar_ds_sindarin true
\charset  tengwar_ds_parmaite false
\charset  tengwar_ds_eldamar  false
\charset  tengwar_ds_annatar  false
\charset  tengwar_freemono    false

\beg      options
  \beg option reverse_o_u_tehtar O_UP_U_DOWN
    \value O_UP_U_DOWN 1
    \value U_UP_O_DOWN 2
  \end

  \beg option consonant_modification_style CONSONANT_MODIFICATION_STYLE_BAR
    \value CONSONANT_MODIFICATION_STYLE_WAVE 0
    \value CONSONANT_MODIFICATION_STYLE_BAR 1
  \end

  \option reverse_numbers true
  \beg option numbers_base BASE_12
    \value    BASE_10 10
    \value    BASE_12 12
  \end

\end

\beg      preprocessor
  \** Work exclusively downcase **\
  \downcase
  
  \** Simplify trema vowels **\
  \substitute "ä" "a"
  \substitute "ë" "e"
  \substitute "ï" "i"
  \substitute "ö" "o"
  \substitute "ü" "u"
  \substitute "ÿ" "y"
  
  \** Dis-ambiguate long vowels **\
  \rxsubstitute "(ā|â|aa)" "á"
  \rxsubstitute "(ē|ê|ee)" "é"
  \rxsubstitute "(ī|î|ii)" "í"
  \rxsubstitute "(ō|ô|oo)" "ó"
  \rxsubstitute "(ū|û|uu)" "ú"
  \rxsubstitute "(ȳ|ŷ|yy)" "ý"
  
  \** Preprocess numbers **\
  \elvish_numbers "\\eval numbers_base" "\\eval reverse_numbers"
\end

\beg      processor

  \beg    rules litteral  
  
    \if "consonant_modification_style == CONSONANT_MODIFICATION_STYLE_WAVE"
      {GEMINATE} === GEMINATE_SIGN_TILD
    \else
      {GEMINATE} === GEMINATE_SIGN
    \endif
  
    {A}   === a
    {AA}  === á
    {E}   === e
    {EE}  === é
    {I}   === i
    {II}  === í
    {O}   === o
    {OO}  === ó
    {U}   === u
    {UU}  === ú

    \** Short diphthongs **\
    {AI}  === {A}{I}
    {AU}  === {A}{U}

    \** LONG diphthongs **\      
    {AAI} === {AA}{I} \** âi **\
    {AAU} === {AA}{U} \** âu **\
    {EEI} === {EE}{I} \** êi **\
    {EEU} === {EE}{U} \** êu **\
    {OOI} === {OO}{I} \** ôi **\
    {OOU} === {OO}{U} \** ôu **\
        
    \if "reverse_o_u_tehtar == U_UP_O_DOWN"
      {O_LOOP} === O_TEHTA
      {U_LOOP} === U_TEHTA
    \else
      {O_LOOP} === U_TEHTA
      {U_LOOP} === O_TEHTA
    \endif
        
    {SDIPHTHONGS}  === {AI}           * {AU}
    {SDIPHTHENGS}  === YANTA A_TEHTA  * URE A_TEHTA 
                   
    {LDIPHTHONGS}  === {AAI}               * {AAU}              * {EEI}              * {EEU}            * {OOI}               * {OOU}
    {LDIPHTHENGS}  === ARA A_TEHTA YANTA   * ARA A_TEHTA URE    * ARA E_TEHTA YANTA  * ARA E_TEHTA URE  * ARA {O_LOOP} YANTA  * ARA {O_LOOP} URE
                                                                                                                 
    {VOWELS}      === {A}          * {E}          * {I}          * {O}          * {U}    
    {_TEHTAR_}    === A_TEHTA      * E_TEHTA      *  I_TEHTA     * {O_LOOP}     * {U_LOOP}
                   
    {LVOWELS}     === {AA}         * {EE}         * {II}         * {OO}         * {UU}
    {LVOWTNG}     === ARA A_TEHTA  * ARA E_TEHTA  * ARA I_TEHTA  * ARA {O_LOOP} * ARA {U_LOOP} 

    \** Let' put all vowels/diphthongs in the same basket **\
    {V_D}         === [ {VOWELS}    * {LVOWELS} * {SDIPHTHONGS} * {LDIPHTHONGS} ]        
    \** And their images... **\            
    {_V_D_}       === [ {_TEHTAR_}  * {LVOWTNG} * {SDIPHTHENGS} * {LDIPHTHENGS} ]
  
    [{VOWELS}]       --> TELCO [{_TEHTAR_}]   \** Replace isolated short vowels **\
    [{LVOWELS}]      --> [{LVOWTNG}]    \** Replace long vowels **\
    [{SDIPHTHONGS}]  --> [{SDIPHTHENGS}]  \** Replace short diphthongs **\
    [{LDIPHTHONGS}]  --> [{LDIPHTHENGS}]  \** Replace long diphthongs **\

    \** ================ **\
    \**    CONSONANTS    **\
    \** ================ **\     
    {K}   === (c,k)
    {V}   === (v,w)

    {L1_S}         === {K}     * p     * t     * {K}{K}            * pp                * tt
    {L1_T}         === QUESSE  * PARMA * TINCO * CALMA {GEMINATE}  * PARMA {GEMINATE}  * TINCO  {GEMINATE}
    
    [{L1_S}]       -->  [ {L1_T} ]
    [{L1_S}]{V_D}  -->  [ {L1_T} ]{_V_D_} 

    {L2_S}         === d    * b     * g     * dd              * bb                * gg
    {L2_T}         === ANDO * UMBAR * UNGWE * ANDO {GEMINATE} * UMBAR {GEMINATE}  * UNGWE {GEMINATE}
    [{L2_S}]       --> [{L2_T}] 
    [{L2_S}]{V_D}  --> [{L2_T}]{_V_D_} 

    \** Alignment of tehta is not the same in the font **\
    \** So we need to split the third line unfortunately **\
    {L3_1_S}          === th    * ph      * (t,th)th          * (p,ph)ph            * (t,th)ph    * (k,kh)ph      * (p,ph)th    * (k,kh)th
    {L3_1_T}          === SULE  * FORMEN  * SULE {GEMINATE}   * FORMEN {GEMINATE}   * SULE FORMEN * HWESTA FORMEN * FORMEN SULE * HWESTA SULE
   
    {L3_2_S}          === sh    * kh      * (k,kh)kh          * (p,ph)kh            * (t,th)kh
    {L3_2_T}          === AHA   * HWESTA  * HWESTA {GEMINATE} * FORMEN HWESTA       * SULE HWESTA
   
    [{L3_1_S}]        --> [{L3_1_T}] 
    [{L3_1_S}]{V_D}   --> [{L3_1_T}]{_V_D_} 
    [{L3_2_S}]        --> [{L3_2_T}] 
    [{L3_2_S}]{V_D}   --> [{L3_2_T}]{_V_D_} 

    {L4_S}            === nd    * mb    * ng
    {L4_T}            === ANTO  * AMPA  * UNQUE
    [{L4_S}]          --> [{L4_T}] 
    [{L4_S}]{V_D}     --> [{L4_T}]{_V_D_} 

    {L5_S}            === n     * m     * nn                 * mm
    {L5_T}            === NUMEN * MALTA * NUMEN {GEMINATE}   * MALTA {GEMINATE}
    [{L5_S}]          --> [{L5_T}] 
    [{L5_S}]{V_D}     --> [{L5_T}]{_V_D_} 

    {L6_S}            === {V}   * y     * rr                 * {V}{V}             * yy
    {L6_T}            === VALA  * ANNA  * ROMEN {GEMINATE}   * VALA {GEMINATE}    * ANNA {GEMINATE}
    [r * {L6_S}]      --> [ ORE   * {L6_T}] 
    [r * {L6_S}]{V_D} --> [ ROMEN * {L6_T}]{_V_D_} 

    \** This one is not useful (redundant with higher) **\
    \** Keep it for clarity of mind **\
    r_                --> ORE

    s{V_D}            --> SILME_NUQUERNA {_V_D_}  \** Before a vowel goes down **\
    s                 --> SILME                   \** Any other pos, up **\
    z{V_D}            --> ESSE_NUQUERNA {_V_D_}   \** Before a vowel goes down **\
    z                 --> ESSE                    \** Any other pos, up **\

    h{V_D}            --> HYARMEN {_V_D_}
    h                 --> HYARMEN
    hh{V_D}           --> HYARMEN {GEMINATE} {_V_D_}
    hh                --> HYARMEN {GEMINATE}
                      
    l{V_D}            --> LAMBE {_V_D_}
    l                 --> LAMBE
                      
    ll{V_D}           --> LAMBE {GEMINATE} {_V_D_}
    ll                --> LAMBE {GEMINATE}
  
  \end
  
  \beg rules punctutation
    . --> PUNCT_DDOT
    .. --> PUNCT_DOT PUNCT_DDOT PUNCT_DOT
    …  --> PUNCT_TILD
    ... --> PUNCT_TILD
    .... --> PUNCT_TILD
    ..... --> PUNCT_TILD
    ...... --> PUNCT_TILD
    ....... --> PUNCT_TILD

    , --> PUNCT_DOT
    : --> PUNCT_DOT
    ; --> PUNCT_DOT
    ! --> PUNCT_EXCLAM
    ? --> PUNCT_INTERR
    · --> PUNCT_DOT

    \** Apostrophe **\

    ' --> {NULL}
    ’ --> {NULL}

    \** Quotes **\

    “ --> DQUOT_OPEN
    ” --> DQUOT_CLOSE
    « --> DQUOT_OPEN 
    » --> DQUOT_CLOSE 
    
    - --> PUNCT_DOT    
    – --> PUNCT_TILD  
    — --> PUNCT_DTILD
 
    [ --> PUNCT_PAREN_L
    ] --> PUNCT_PAREN_R
    ( --> PUNCT_PAREN_L
    ) --> PUNCT_PAREN_R
    { --> PUNCT_PAREN_L
    } --> PUNCT_PAREN_R
    < --> PUNCT_PAREN_L
    > --> PUNCT_PAREN_R  

    \** Not universal between fonts ... **\
    $ --> BOOKMARK_SIGN
    ≤ --> RING_MARK_L \** Ring inscription left beautiful stuff **\
    ≥ --> RING_MARK_R \** Ring inscription right beautiful stuff **\
  \end

  \beg rules numbers
    0 --> NUM_0
    1 --> NUM_1
    2 --> NUM_2
    3 --> NUM_3
    4 --> NUM_4
    5 --> NUM_5
    6 --> NUM_6
    7 --> NUM_7
    8 --> NUM_8
    9 --> NUM_9
    A --> NUM_10
    B --> NUM_11   
  \end
  
\end

\beg postprocessor
  \resolve_virtuals
\end
